/obj/item/clothing/cloak/stabard/mercenary/Initialize()
	. = ..()
	if(color && detail_color)
		return
	var/list/dye_colors = GLOB.peasant_dyes + GLOB.noble_dyes + GLOB.royal_dyes
	if(!color)
		color = pick_assoc(dye_colors)
	if(!detail_color)
		detail_color = pick_assoc(dye_colors)
	update_appearance(UPDATE_ICON)
	if(ismob(loc))
		var/mob/L = loc
		L.update_inv_cloak()

// UPSTREAM BUG (remove this override once fixed upstream): /obj/structure/vine/Crossed types the crosser as /mob and reads crosser.m_intent, but non-mob movers (e.g. the wandering /obj/item/reagent_containers/food/snacks/smallrat) cross vines and runtime on the undefined var. Reimplemented with an ismob() guard; replicates /atom/movable/Crossed (COMSIG_MOVABLE_CROSSED) and /obj/structure/Crossed (climb offset) because the same-type redefinition bypasses the upstream ..() chain.
/obj/structure/vine/Crossed(atom/movable/crosser, oldloc)
	SEND_SIGNAL(src, COMSIG_MOVABLE_CROSSED, crosser)
	if(isliving(crosser) && !crosser.throwing && climb_offset)
		var/mob/living/climber = crosser
		climber.add_offsets("structure_climb", x_add = 0, y_add = climb_offset)
	if(ismob(crosser))
		var/mob/mob_crosser = crosser
		if(mob_crosser.m_intent != MOVE_INTENT_SNEAK)
			playsound(src, 'sound/items/seedextract.ogg', 80, TRUE, -1)
	if(isliving(crosser))
		for(var/datum/vine_mutation/SM in mutations)
			SM.on_cross(src, crosser)
	if(prob(23) && ismob(crosser) && !isvineimmune(crosser))
		var/mob/living/M = crosser
		if(iscarbon(M))
			var/mob/living/carbon/H = M
			var/obj/item/bodypart/l_leg/left = H.get_bodypart(BODY_ZONE_L_LEG)
			var/obj/item/bodypart/r_leg/right = H.get_bodypart(BODY_ZONE_R_LEG)
			if(prob(50))
				left.bodypart_attacked_by(BCLASS_CUT, 6)
			else
				right.bodypart_attacked_by(BCLASS_CUT, 6)
		else
			M.adjustBruteLoss(5)
		to_chat(M, span_warning("I nick myself on the thorny vines."))

#if defined(UNIT_TESTS) || defined(SPACEMAN_DMM)
/datum/unit_test/turf_coverage/Run()
	var/list/all_turfs = subtypesof(/turf)
	var/list/all_blueprint_recipes = subtypesof(/datum/blueprint_recipe)
	var/list/used_turfs = list()

	for(var/recipe_type in all_blueprint_recipes)
		var/datum/blueprint_recipe/recipe = new recipe_type()
		if(recipe.result_type && ispath(recipe.result_type, /turf))
			used_turfs |= recipe.result_type
		qdel(recipe)

	var/list/blacklisted_turfs = list(
		/turf/closed,
		/turf/closed/splashscreen,
		/turf/open/floor,
		/turf/open,
		/turf/open/floor/grass/hell,
		/turf/open/floor/grass/eora,
		/turf/open/floor/dirt/ambush,
		/turf/open/floor/cobble/snow,
		/turf/open/floor/volcanic,
		/turf/open/floor/blocks/snow,
		/turf/open/openspace,
		/turf/baseturf_skipover,
		/turf/baseturf_bottom,
		/turf/closed/basic,
		/turf/open/floor/cobblerock/snow,
		/turf/open/floor/plasteel,
		/turf/open/floor/naturalstone,
		/turf/open/floor/plank/h,
		/turf/open/floor/plank,
		/turf/closed/wall,
		/turf/open/floor/sandstone,
		/turf/closed/dungeon_void,
		/turf/closed/sea_fog,
		/turf/template_noop,
		/turf/closed/wall/mineral/underbrick/fake_world,
		/turf/closed/wall/mineral,
		/turf/closed/wall/mineral/stonebrick/reddish,
		/turf/closed/wall/mineral/decostone/cand/reddish,
		/obj/structure/stairs/stone/reddish,
		/turf/closed/wall/mineral/roofwall,
		/turf/closed/wall/mineral/abyssal,
		/turf/closed/wall/mineral/desert_soapstone,
		/turf/open/floor/cracked_earth,
		/turf/open/floor/flesh,
		/turf/open/dungeon_trap,
		/turf/open/dungeon_trap/australia,
		/turf/open/floor/blocks/carved,
		/turf/open/floor/churchmarble/purple,
		/turf/open/floor/churchmarble/violet,
		/turf/open/floor/churchmarble/rust,
		/turf/open/floor/churchmarble/pale,
		/turf/open/floor/churchmarble/gold,
		/turf/open/floor/churchmarble/green,
		/turf/open/floor/church/violet,
		/turf/open/floor/church/rust,
		/turf/open/floor/church/pale,
		/turf/open/floor/church/gold,
		/turf/open/floor/church/green,
		/turf/open/floor/churchrough/violet,
		/turf/open/floor/churchrough/rust,
		/turf/open/floor/churchrough/pale,
		/turf/open/floor/churchrough/gold,
		/turf/open/floor/churchrough/green,
		/turf/open/floor/cobble/dun_world,
		/turf/open/floor/cobble/mossy/dun_world,
		/turf/open/floor/cobblerock/dun_world,
		/turf/open/floor/hexstone/dun_world,
		/turf/open/floor/churchrough/dun_world,
		/turf/open/floor/church/dun_world,
	) \
	+ typesof(/turf/open/floor/mushroom) \
	+ typesof(/turf/open/floor/sandstone_tile) \
	+ typesof(/turf/open/floor/abyss_sand) \
	+ typesof(/turf/open/floor/sand) \
	+ typesof(/turf/open/floor/abyss_tile) \
	+ typesof(/turf/closed/indestructible) \
	+ typesof(/turf/closed/wall/mineral/decostone/fluffstone) \
	+ typesof(/turf/open/floor/plasteel/maniac) \
	+ typesof(/turf/closed/mineral) \
	+ typesof(/turf/open/floor/underworld) \
	+ typesof(/turf/open/floor/snow) \
	+ typesof(/turf/open/floor/woodturned/nosmooth) \
	+ typesof(/turf/open/floor/wood/nosmooth) \
	+ typesof(/turf/open/water) \
	+ typesof(/turf/open/lava) \
	+ typesof(/turf/open/floor/carpet) \
	+ typesof(/turf/closed/wall/mineral/desert_sandstone) \
	+ typesof(/turf/closed/wall/mineral/roofwall) \
	+ typesof(/turf/closed/wall/mineral/decostone/dun_world)
	used_turfs |= blacklisted_turfs

	var/list/unused_turfs = list()
	for(var/turf_type in all_turfs)
		if(!(turf_type in used_turfs))
			unused_turfs += turf_type

	var/unused_list = ""
	for(var/i = 1; i <= unused_turfs.len; i++)
		unused_list += "[unused_turfs[i]]"
		if(i < unused_turfs.len)
			unused_list += ", "

	if(length(unused_turfs))
		return Fail("Assertion failed: The following turfs are not used by any blueprint recipe or in the blacklist: [unused_list]", __FILE__, __LINE__)

/datum/unit_test/weapon_icons/Run()
	for(var/obj/item/weapon/checked as anything in subtypesof(/obj/item/weapon))
		if(IS_ABSTRACT(checked))
			continue
		if(checked == /obj/item/weapon/sword/rapier/dun_world_lord)
			continue
		checked = allocate(checked)
		if(checked.icon_state && !icon_exists(checked.icon, checked.icon_state))
			var/icon_file = "[checked.icon]" || "Unknown Generated Icon"
			Fail("Invalid icon_state: Icon object '[icon_file]' [REF(checked.icon)] used in '[checked]' [checked.type] is missing icon state [checked.icon_state].", __FILE__, __LINE__)
#endif
