/datum/erp_vfx_service
	var/datum/erp_controller/controller

/datum/erp_vfx_service/New(datum/erp_controller/C)
	. = ..()
	controller = C

/datum/erp_vfx_service/proc/get_arousal_value(mob/living/carbon/human/H)
	if(!istype(H))
		return null

	var/list/data = list()
	SEND_SIGNAL(H, COMSIG_SEX_GET_AROUSAL, data)
	if(!length(data))
		return null

	return data["arousal"]

/datum/erp_vfx_service/proc/build_tick_effect_bundle(list/active_links, dt)
	var/list/E = list()
	E["thrust_link"] = null
	E["suck_link"] = null
	E["slap_link"] = null
	E["hearts_mobs"] = list()
	for(var/datum/erp_sex_link/L in active_links)
		if(!L || QDELETED(L) || !L.is_valid())
			continue

		var/init_t = L.init_organ?.erp_organ_type
		var/tgt_t  = L.target_organ?.erp_organ_type
		if(!E["suck_link"] && is_sucking_pair(init_t, tgt_t))
			E["suck_link"] = L

		if(!E["slap_link"])
			var/list/tags = L.action?.action_tags
			if(islist(tags) && ("spanking" in tags))
				E["slap_link"] = L

		if(!E["thrust_link"] && is_thrust_init(init_t))
			E["thrust_link"] = L

	for(var/datum/erp_sex_link/L2 in active_links)
		if(!L2 || QDELETED(L2) || !L2.is_valid())
			continue
		var/mob/living/carbon/human/A = L2.actor_active?.get_effect_mob()
		var/mob/living/carbon/human/B = L2.actor_passive?.get_effect_mob()

		if(istype(A))
			var/arA = get_arousal_value(A)
			if(isnum(arA) && arA >= 20)
				E["hearts_mobs"][A] = TRUE

		if(istype(B))
			var/arB = get_arousal_value(B)
			if(isnum(arB) && arB >= 20)
				E["hearts_mobs"][B] = TRUE

	return E

/datum/erp_vfx_service/proc/play_tick_effects(list/active_links, dt)
	if(controller.hidden_mode)
		return

	var/list/E = build_tick_effect_bundle(active_links, dt)
	if(!islist(E))
		return

	var/datum/erp_sex_link/suckL = E["suck_link"]
	if(suckL)
		var/mob/living/carbon/human/mouth_mob = get_mouth_mob_for_link(suckL)
		if(istype(mouth_mob))
			play_suck(mouth_mob, suckL)

	var/datum/erp_sex_link/slapL = E["slap_link"]
	if(slapL)
		var/mob/living/carbon/human/slap_mob = slapL.actor_active?.get_effect_mob()
		if(istype(slap_mob))
			play_slap(slap_mob)

	var/datum/erp_sex_link/thrustL = E["thrust_link"]
	if(thrustL)
		do_thrust_bump(thrustL)
		var/mob/living/carbon/human/thrust_mob = thrustL.actor_active?.get_effect_mob()
		if(istype(thrust_mob))
			do_onomatopoeia(thrust_mob, thrustL)
			play_thrust_sound(thrust_mob, thrustL)

	var/list/HM = E["hearts_mobs"]
	if(islist(HM))
		for(var/mob/living/carbon/human/H as anything in HM)
			if(istype(H))
				spawn_hearts(H)

/datum/erp_vfx_service/proc/do_thrust_bump(datum/erp_sex_link/best)
	if(!best || QDELETED(best) || !best.is_valid())
		return

	var/mob/living/user = best.actor_active?.get_effect_mob()
	var/atom/movable/target = get_best_thrust_target(best)
	if(!user || !target)
		return

	var/force = clamp(round(best.force || SEX_FORCE_MID), SEX_FORCE_LOW, SEX_FORCE_EXTREME)
	var/speed = clamp(round(best.speed || SEX_SPEED_MID), SEX_SPEED_LOW, SEX_SPEED_EXTREME)

	var/pixels = 3 + (force - SEX_FORCE_LOW)
	pixels = clamp(pixels, 2, 7)

	var/time = 3.4 - (speed * 0.35)
	time = clamp(time, 1.6, 3.6)

	do_thrust_animate(user, target, null, pixels, time)
	try_furniture_shake(best, user, target, time)

/datum/erp_vfx_service/proc/get_best_thrust_target(datum/erp_sex_link/best)
	if(!best)
		return null

	var/mob/living/A = best.actor_active?.get_effect_mob()
	var/mob/living/B = best.actor_passive?.get_effect_mob()
	if(!A || !B)
		return null

	return B

/datum/erp_vfx_service/proc/do_onomatopoeia(mob/living/carbon/human/user, datum/erp_sex_link/best)
	if(!istype(user))
		return

	var/t_init = best?.init_organ?.erp_organ_type
	var/t_tgt  = best?.target_organ?.erp_organ_type

	var/msg = "Plap!"

	if(t_init == SEX_ORGAN_MOUTH && t_tgt == SEX_ORGAN_MOUTH)
		msg = pick("Mwah!", "Kiss!")
	else
		if(t_init == SEX_ORGAN_MOUTH)
			if(t_tgt == SEX_ORGAN_PENIS)
				msg = pick("Slurp!", "Suck!")
			else if(t_tgt == SEX_ORGAN_VAGINA || t_tgt == SEX_ORGAN_ANUS)
				msg = pick("Lick!", "Slurp!")
			else if(t_tgt == SEX_ORGAN_BREASTS)
				msg = pick("Suck!", "Mmm!")
			else
				msg = pick("Mwah!", "Smack!")

	user.balloon_alert_to_viewers(msg)

/datum/erp_vfx_service/proc/play_slap(mob/living/carbon/human/user)
	if(!istype(user))
		return

	var/sound = pick('modular_abel/sound/foley/slap.ogg', 'sound/foley/smackspecial.ogg')
	playsound(user, sound, 50, TRUE, -2, ignore_walls = FALSE)

/datum/erp_vfx_service/proc/link_is_sucking(datum/erp_sex_link/L)
	if(!L || QDELETED(L) || !L.is_valid())
		return FALSE

	var/datum/erp_sex_organ/init = L.init_organ
	var/datum/erp_sex_organ/tgt  = L.target_organ
	if(!init || !tgt)
		return FALSE

	if(init.erp_organ_type != SEX_ORGAN_MOUTH)
		return FALSE

	if(tgt.erp_organ_type == SEX_ORGAN_MOUTH)
		return TRUE

	return (tgt.erp_organ_type in list(SEX_ORGAN_VAGINA, SEX_ORGAN_BREASTS, SEX_ORGAN_PENIS, SEX_ORGAN_ANUS))

/datum/erp_vfx_service/proc/play_suck(mob/living/carbon/human/user, datum/erp_sex_link/best)
	if(!istype(user) || !best)
		return

	if(user.gender == FEMALE)
		playsound(user,
			pick('modular_abel/sound/misc/mat/girlmouth (1).ogg','modular_abel/sound/misc/mat/girlmouth (2).ogg','modular_abel/sound/misc/mat/oral (1).ogg','modular_abel/sound/misc/mat/oral (2).ogg','modular_abel/sound/misc/mat/oral (3).ogg','modular_abel/sound/misc/mat/oral (4).ogg','modular_abel/sound/misc/mat/oral (5).ogg','modular_abel/sound/misc/mat/oral (6).ogg','modular_abel/sound/misc/mat/oral (7).ogg'),
			25, TRUE,
			ignore_walls = FALSE
		)
	else
		playsound(user,
			pick('modular_abel/sound/misc/mat/guymouth (2).ogg','modular_abel/sound/misc/mat/guymouth (3).ogg','modular_abel/sound/misc/mat/guymouth (4).ogg','modular_abel/sound/misc/mat/guymouth (5).ogg','modular_abel/sound/misc/mat/oral (1).ogg','modular_abel/sound/misc/mat/oral (2).ogg','modular_abel/sound/misc/mat/oral (3).ogg','modular_abel/sound/misc/mat/oral (4).ogg','modular_abel/sound/misc/mat/oral (5).ogg','modular_abel/sound/misc/mat/oral (6).ogg','modular_abel/sound/misc/mat/oral (7).ogg'),
			35, TRUE,
			ignore_walls = FALSE
		)

	var/volume_layer = 12
	switch(clamp(round(best.force || SEX_FORCE_MID), SEX_FORCE_LOW, SEX_FORCE_EXTREME))
		if(SEX_FORCE_LOW)
			return
		if(SEX_FORCE_HIGH)
			volume_layer = 14
		if(SEX_FORCE_EXTREME)
			volume_layer = 16

	playsound(user,
		pick('modular_abel/sound/misc/mat/saliva (1).ogg','modular_abel/sound/misc/mat/saliva (2).ogg','modular_abel/sound/misc/mat/saliva (3).ogg'),
		volume_layer, TRUE, -2,
		ignore_walls = FALSE
	)

/datum/erp_vfx_service/proc/play_thrust_sound(mob/living/carbon/human/user, datum/erp_sex_link/best)
	if(!istype(user) || !best)
		return

	var/action_force = clamp(round(best.force || SEX_FORCE_MID), SEX_FORCE_LOW, SEX_FORCE_EXTREME)
	var/sound

	switch(action_force)
		if(SEX_FORCE_LOW, SEX_FORCE_MID)
			sound = pick(SEX_SOUNDS_SLOW)
		if(SEX_FORCE_HIGH, SEX_FORCE_EXTREME)
			sound = pick(SEX_SOUNDS_HARD)

	if(sound)
		playsound(user, sound, 30, TRUE, -2, ignore_walls = FALSE)

/datum/erp_vfx_service/proc/spawn_hearts(mob/living/carbon/human/user)
	if(!istype(user))
		return

	for(var/i in 1 to rand(1, 3))
		if(!user.cmode)
			new /obj/effect/temp_visual/heart/sex_effects(get_turf(user))
		else
			new /obj/effect/temp_visual/heart/sex_effects/red_heart(get_turf(user))

/datum/erp_vfx_service/proc/zone_key_to_bodyzone(zone)
	switch(zone)
		if("groin") return BODY_ZONE_PRECISE_GROIN
		if("chest") return BODY_ZONE_CHEST
		if("mouth") return BODY_ZONE_PRECISE_MOUTH
	return null

/datum/erp_vfx_service/proc/try_furniture_shake(datum/erp_sex_link/L, mob/living/user, atom/movable/target, time)
	if(!L || QDELETED(L) || !L.is_valid())
		return
	if(!user || !target)
		return

	var/force = clamp(round(L.force || SEX_FORCE_MID), SEX_FORCE_LOW, SEX_FORCE_EXTREME)
	if(force <= SEX_FORCE_MID)
		return

	var/atom/movable/furniture = L.get_furniture_for_scene()
	if(!furniture)
		return

	shake_furniture(furniture, force, time)

/datum/erp_vfx_service/proc/shake_furniture(atom/movable/furniture, force, time)
	if(!furniture || QDELETED(furniture))
		return
	if(time <= 0)
		return

	var/shake_force = clamp(round(force || SEX_FORCE_MID), SEX_FORCE_LOW, SEX_FORCE_EXTREME)
	if(shake_force <= SEX_FORCE_MID)
		return

	var/old_pixel_y = furniture.pixel_y
	var/pixel_shift = 1

	switch(shake_force)
		if(SEX_FORCE_HIGH)
			pixel_shift = 2
		if(SEX_FORCE_EXTREME)
			pixel_shift = 3
		else
			pixel_shift = 1

	var/half_time = max(1, round(time / 2))
	var/target_pixel_y = old_pixel_y - pixel_shift

	animate(furniture, pixel_y = target_pixel_y, time = half_time)
	animate(pixel_y = old_pixel_y, time = half_time)

	apply_furniture_side_effects(furniture, shake_force)

/datum/erp_vfx_service/proc/apply_furniture_side_effects(atom/movable/furniture, force)
	if(!furniture || QDELETED(furniture))
		return
	if(!isobj(furniture))
		return

	var/obj/O = furniture
	var/damage_amount = get_furniture_damage_from_force(force)
	if(damage_amount <= 0)
		return

	damage_furniture_safely(O, damage_amount, force)

/datum/erp_vfx_service/proc/get_safe_furniture_damage_cap(obj/O)
	if(!O || QDELETED(O))
		return 0

	if(!O.max_integrity)
		return 0

	var/min_safe_integrity
	if(O.integrity_failure)
		min_safe_integrity = (O.integrity_failure * O.max_integrity) + DAMAGE_PRECISION
	else
		min_safe_integrity = DAMAGE_PRECISION

	var/safe_damage_cap = O.get_integrity() - min_safe_integrity
	return max(0, round(safe_damage_cap, DAMAGE_PRECISION))

/datum/erp_vfx_service/proc/get_furniture_damage_from_force(force)
	if(force > SEX_FORCE_HIGH)
		return 2
	if(force > SEX_FORCE_MID)
		return 1
	return 0

/datum/erp_vfx_service/proc/play_furniture_erp_sound(atom/movable/furniture, force)
	if(!furniture || QDELETED(furniture))
		return FALSE

	if(istype(furniture, /obj/structure/bed))
		if(force > SEX_FORCE_MID)
			playsound(
				furniture,
				pick(list(
					'modular_abel/sound/misc/mat/bed squeak (1).ogg',
					'modular_abel/sound/misc/mat/bed squeak (2).ogg',
					'modular_abel/sound/misc/mat/bed squeak (3).ogg'
				)),
				30,
				TRUE,
				ignore_walls = FALSE
			)
			return TRUE

	return FALSE

/datum/erp_vfx_service/proc/damage_furniture_safely(obj/O, damage_amount, force)
	if(!O || QDELETED(O))
		return
	if(damage_amount <= 0)
		return

	var/safe_cap = get_safe_furniture_damage_cap(O)
	if(safe_cap <= 0)
		return

	var/final_damage = min(damage_amount, safe_cap)
	if(final_damage < DAMAGE_PRECISION)
		return

	var/played_custom_sound = play_furniture_erp_sound(O, force)

	O.take_damage(final_damage, BRUTE, "blunt", !played_custom_sound)

/datum/erp_vfx_service/proc/is_sucking_pair(init_t, tgt_t)
	return (init_t == SEX_ORGAN_MOUTH) || (tgt_t == SEX_ORGAN_MOUTH)

/datum/erp_vfx_service/proc/get_mouth_mob_for_link(datum/erp_sex_link/L)
	if(!L)
		return null
	if(L.init_organ?.erp_organ_type == SEX_ORGAN_MOUTH)
		return L.actor_active?.get_effect_mob()
	if(L.target_organ?.erp_organ_type == SEX_ORGAN_MOUTH)
		return L.actor_passive?.get_effect_mob()
	return null

/datum/erp_vfx_service/proc/is_thrust_init(init_t)
	return (init_t in list(SEX_ORGAN_MOUTH, SEX_ORGAN_VAGINA, SEX_ORGAN_PENIS, SEX_ORGAN_TAIL))

/datum/erp_vfx_service/proc/find_closet_for_thrust(datum/erp_sex_link/L, mob/living/user, atom/movable/target)  
    var/atom/movable/A = L?.actor_active?.get_movable()  
    var/atom/movable/B = L?.actor_passive?.get_movable()  

    if(A?.loc == B?.loc && istype(A.loc, /obj/structure/closet))  
        return A.loc  

    return null 

/datum/erp_vfx_service/proc/shake_closet(obj/structure/closet/C, force, time)
	if(!C || QDELETED(C))
		return

	var/oldx = C.pixel_x
	var/push = 1
	if(force >= SEX_FORCE_EXTREME)
		push = 3
	else if(force >= SEX_FORCE_HIGH)
		push = 2

	var/target_x = oldx + pick(-push, push)
	var/t = max(1, round(time / 2))
	animate(C, pixel_x = target_x, time = t)
	animate(pixel_x = oldx, time = t)
