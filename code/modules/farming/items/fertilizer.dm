/obj/item/fertilizer
	name = "generic fertilizer"
	desc = "A basic fertilizer."
	w_class = WEIGHT_CLASS_SMALL
	grid_width = 32
	grid_height = 32

	var/nitrogen_content = 20
	var/phosphorus_content = 20
	var/potassium_content = 20

/obj/item/fertilizer/examine(mob/user)
	. = ..()
	if(GET_MOB_SKILL_VALUE_OLD(user, /datum/attribute/skill/labor/farming) >= 3)
		if(nitrogen_content)
			. += span_info("Restores [nitrogen_content] Nitrogen")
		if(phosphorus_content)
			. += span_info("Restores [phosphorus_content] Phosphorus")
		if(potassium_content)
			. += span_info("Restores [potassium_content] Potassium")

/obj/item/fertilizer/bone_meal
	name = "bone meal"
	desc = "Crushed bones, perfect for the garden."
	icon = 'icons/roguetown/misc/alchemy.dmi'
	icon_state = "bonemeal"
	nitrogen_content = 5
	phosphorus_content = 45
	potassium_content = 10

/obj/item/fertilizer/compost
	name = "compost"
	desc = "Decomposed produce ready to give life to plants."
	icon = 'icons/roguetown/misc/composter.dmi'
	icon_state = "compost"
	nitrogen_content = 30
	phosphorus_content = 15
	potassium_content = 25

/obj/item/fertilizer/ash
	name = "ash"
	desc = "A handful of soot."
	icon = 'icons/obj/objects.dmi'
	icon_state = "ash"
	w_class = WEIGHT_CLASS_TINY
	nitrogen_content = 0
	phosphorus_content = 20
	potassium_content = 50

/obj/item/fertilizer/ash/Initialize(mapload)
	. = ..()
	create_reagents(50)
	reagents.add_reagent(/datum/reagent/ash, 50)

/obj/item/fertilizer/ash/burn()
	if(resistance_flags & ON_FIRE)
		SSfire_burning.processing -= src
	deconstruct(FALSE)

/obj/item/fertilizer/ash/Crossed(mob/living/L)
	. = ..()
	if(istype(L))
		var/prob2break = 33
		if(L.m_intent == MOVE_INTENT_SNEAK)
			prob2break = 0
		if(L.m_intent == MOVE_INTENT_RUN)
			prob2break = 100
		if(prob(prob2break))
			qdel(src)

/obj/item/fertilizer/ash/attackby(obj/item/attacking_item, mob/living/user)
	if(istype(attacking_item, /obj/item/reagent_containers/glass))
		var/obj/item/reagent_containers/glass = attacking_item
		if(attacking_item.reagents && glass.is_open_container())
			. = 1 //so the containers don't splash their content on the src while scooping.
			if(glass.reagents.total_volume >= glass.reagents.maximum_volume)
				to_chat(user, "<span class='notice'>[glass] is full!</span>")
				return
			to_chat(user, "<span class='notice'>I scoop up [src] into [glass]!</span>")
			reagents.trans_to(glass, reagents.total_volume, transfered_by = user)
			if(!reagents.total_volume) //scooped up all of it
				qdel(src)
				return
	else
		return ..()

/obj/item/fertilizer/ash/large
	name = "large pile of ashes"
	icon_state = "big_ash"
	w_class = WEIGHT_CLASS_NORMAL
	phosphorus_content = 40
	potassium_content = 100
