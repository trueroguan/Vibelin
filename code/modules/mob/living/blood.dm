/****************************************************
				BLOOD SYSTEM
****************************************************/

/mob/living/proc/suppress_bloodloss(amount)
	if(bleedsuppress)
		return
	else
		bleedsuppress = TRUE
		addtimer(CALLBACK(src, PROC_REF(resume_bleeding)), amount)

/mob/living/proc/resume_bleeding()
	bleedsuppress = 0
	if(stat != DEAD && bleed_rate)
		to_chat(src, span_warning("The blood soaks through my bandage."))

/mob/living/carbon/monkey/handle_blood()
	if(HAS_TRAIT(src, TRAIT_HUSK)) //cryosleep or husked people do not pump the blood.
		return
	//Blood regeneration if there is some space
	if(blood_volume < BLOOD_VOLUME_NORMAL && !bleed_rate)
		blood_volume += 0.1 // regenerate blood VERY slowly
		if((blood_volume < BLOOD_VOLUME_OKAY) && !HAS_TRAIT(src, TRAIT_BLOODLOSS_IMMUNE))
			adjustOxyLoss(round((BLOOD_VOLUME_NORMAL - blood_volume) * 0.02, 1))

/mob/living/proc/handle_blood()
	if(HAS_TRAIT(src, TRAIT_HUSK)) //cryosleep or husked people do not pump the blood.
		return
	blood_volume = min(blood_volume, BLOOD_VOLUME_MAX_LETHAL)

	bleed_rate = min(get_bleed_rate(), 10)

	if(blood_volume < BLOOD_VOLUME_NORMAL && blood_volume && !bleed_rate)
		blood_volume = min(blood_volume+0.5, BLOOD_VOLUME_MAX_LETHAL)

	//Effects of bloodloss
	if(!HAS_TRAIT(src, TRAIT_BLOODLOSS_IMMUNE) && stat != DEAD)
		switch(blood_volume)
			if(BLOOD_VOLUME_OKAY to BLOOD_VOLUME_SAFE)
				if(prob(3))
					to_chat(src, span_warning("I feel dizzy."))
				remove_status_effect(/datum/status_effect/debuff/bleedingworse)
				remove_status_effect(/datum/status_effect/debuff/bleedingworst)
				apply_status_effect(/datum/status_effect/debuff/bleeding)
			if(BLOOD_VOLUME_BAD to BLOOD_VOLUME_OKAY)
				if(prob(3))
					set_eye_blur_if_lower(12 SECONDS)
					to_chat(src, span_warning("I feel faint."))
				remove_status_effect(/datum/status_effect/debuff/bleeding)
				remove_status_effect(/datum/status_effect/debuff/bleedingworst)
				apply_status_effect(/datum/status_effect/debuff/bleedingworse)
			if(0 to BLOOD_VOLUME_BAD)
				if(prob(3))
					set_eye_blur_if_lower(12 SECONDS)
					to_chat(src, span_warning("I feel faint."))
				if(prob(3) && stat < UNCONSCIOUS)
					Unconscious(rand(5 SECONDS,10 SECONDS))
					to_chat(src, span_warning("I feel drained."))
				remove_status_effect(/datum/status_effect/debuff/bleedingworse)
				remove_status_effect(/datum/status_effect/debuff/bleeding)
				apply_status_effect(/datum/status_effect/debuff/bleedingworst)
		if(blood_volume <= BLOOD_VOLUME_BAD)
			adjustOxyLoss(2)
			if(blood_volume <= BLOOD_VOLUME_SURVIVE)
				adjustOxyLoss(4)
	else
		remove_status_effect(/datum/status_effect/debuff/bleeding)
		remove_status_effect(/datum/status_effect/debuff/bleedingworse)
		remove_status_effect(/datum/status_effect/debuff/bleedingworst)

	if(bleed_rate)
		bleed(bleed_rate)

	//if(blood_volume in -INFINITY to BLOOD_VOLUME_SURVIVE)
		//adjustOxyLoss(1.6)

// Takes care blood loss and regeneration
/mob/living/carbon/handle_blood()
	return // we handle this in our organs now

/mob/living/proc/get_bleed_rate()
	var/bleed_rate = 0
	for(var/datum/wound/wound as anything in get_wounds())
		bleed_rate += wound.bleed_rate
	for(var/obj/item/embedded as anything in simple_embedded_objects)
		bleed_rate += embedded.embedding?.embedded_bloodloss
	return bleed_rate

/mob/living/carbon/get_bleed_rate()
	if(NOBLOOD in dna?.species?.species_traits)
		return 0
	var/bleed_rate = 0
	for(var/obj/item/bodypart/bodypart as anything in bodyparts)
		bleed_rate += bodypart.get_bleed_rate()
	return bleed_rate

/// How much slower we'll be bleeding for every CON point. 0.1 = 10% slower.
#define CONSTITUTION_BLEEDRATE_MOD 0.03

/// Makes a blood drop, leaking amt units of blood from the mob
/mob/living/proc/bleed(amt)
	if(!iscarbon(src) && !HAS_TRAIT(src, TRAIT_SIMPLE_WOUNDS))
		return
	if(blood_volume <= 0)
		return

	// For each CON above 10, we bleed slower.
	// Consequently, for each CON under 10 we bleed faster.
	var/con_modifier = 1
	var/our_con = GET_MOB_ATTRIBUTE_VALUE(src, STAT_CONSTITUTION)
	if(our_con != 10)
		con_modifier = our_con - 10

	amt -= amt * con_modifier * CONSTITUTION_BLEEDRATE_MOD

	blood_volume = max(blood_volume - amt, 0)

	if(client)
		record_featured_stat(FEATURED_STATS_BLEEDERS, src)
	record_round_statistic(STATS_BLOOD_SPILT, amt / 100)

	if(amt > 1)
		if(isturf(loc)) // Blood loss still happens in locker, floor stays clean
			add_drip_floor(get_turf(src), amt)

		if(body_position != LYING_DOWN && !stat)
			playsound(src, pick('sound/misc/bleed (1).ogg', 'sound/misc/bleed (2).ogg', 'sound/misc/bleed (3).ogg'), 100, FALSE)

	updatehealth()

	return TRUE

#undef CONSTITUTION_BLEEDRATE_MOD

/mob/living/carbon/human/bleed(amt)
	if(NOBLOOD in dna?.species?.species_traits)
		return FALSE
	if(physiology)
		amt *= physiology.bleed_mod
	return ..()

/mob/living/proc/restore_blood()
	blood_volume = initial(blood_volume)

/mob/living/carbon/human/restore_blood()
	blood_volume = BLOOD_VOLUME_NORMAL
	bleed_rate = 0

/****************************************************
				BLOOD TRANSFERS
****************************************************/

//Gets blood from mob to a container or other mob, preserving all data in it.
/mob/living/proc/transfer_blood_to(atom/movable/AM, amount, forced)
	var/datum/blood_type/blood = get_blood_type()
	if(isnull(blood) || !AM.reagents)
		return 0
	if(blood_volume < BLOOD_VOLUME_BAD && !forced)
		return 0

	if(blood_volume < amount)
		amount = blood_volume

	adjust_bloodvolume(-amount)

	AM.reagents.add_reagent(blood.reagent_type, amount, blood.get_blood_data(src), bodytemperature)
	return 1

/// Transfers the blood of a mob factoring in the impure reagents in their blood
/// Returns the actual amount of blood transferred
/mob/living/proc/transfer_blood_impurities(datum/reagents/transfer_to, amount, impurity_mult = BLOODLETTING_MULT, mob/transferred_by)
	var/blacklisted_reagents = list(/datum/reagent/steam, /datum/reagent/water, /datum/reagent/blood, /datum/reagent/consumable/nutriment, /datum/reagent/consumable/soup)
	var/blood_purity = 1 // what % of the amt are we actually taking as blood?
	amount = min(amount, transfer_to.maximum_volume - transfer_to.total_volume) // the volume of our transfer
	if(reagents.total_volume)
		var/impurity_volume = reagents.total_volume
		for(var/reagent_type in blacklisted_reagents)
			impurity_volume -= reagents.get_reagent_amount(reagent_type, FALSE)
		if(impurity_volume > 0)
			blood_purity = blood_volume / (blood_volume + impurity_volume)
			reagents.trans_to(transfer_to, amount * impurity_mult * (1 - blood_purity), transfered_by=transferred_by, ignored_reagents=blacklisted_reagents)
	var/blood_transferred = min(blood_volume, amount * blood_purity)  // how much of the drip is straight up blood, final value
	var/datum/blood_type/blood = get_blood_type()
	var/list/blood_data = blood?.get_blood_data(src)
	var/datum/reagents/holder = new(maximum = blood_transferred)
	// if someone adds kool aid as a blood type then blood_data here might need some work
	holder.add_reagent(blood.reagent_type, blood_transferred, blood_data, no_react = TRUE)
	holder.trans_to(transfer_to, blood_transferred, method = INGEST)
	return blood_transferred


/mob/living/proc/get_blood_type()
	RETURN_TYPE(/datum/blood_type)
	return GLOB.blood_types[animal_type] || GLOB.blood_types[/datum/blood_type/animal]

/mob/living/proc/get_lux_status()
	var/datum/blood_type/blood = get_blood_type()

	if(has_status_effect(/datum/status_effect/debuff/lux_drained) || has_status_effect(/datum/status_effect/debuff/flaw_lux_taken))//accounts for luxless flaw
		return LUX_DRAINED

	if(has_status_effect(/datum/status_effect/debuff/tainted_lux) || has_status_effect(/datum/status_effect/debuff/received_tainted_lux) || has_status_effect(/datum/status_effect/buff/received_lux))
		return LUX_HAS_LUX

	return blood.contains_lux

/mob/living/proc/get_lux_tainted_status()
	if(HAS_TRAIT(src, TRAIT_TAINTED_LUX))
		return TRUE
	var/datum/blood_type/blood = get_blood_type()
	return blood.tainted_lux

/mob/living/carbon/human/get_blood_type()
	RETURN_TYPE(/datum/blood_type)
	if(HAS_TRAIT(src, TRAIT_HUSK) || isnull(dna))
		return null
	if(dna.species.exotic_bloodtype)
		return GLOB.blood_types[dna.species.exotic_bloodtype]
	return GLOB.blood_types[dna.human_blood_type]

// This has more potential uses, and is probably faster than the old proc.
/proc/get_safe_blood(bloodtype)
	. = list()
	if(!bloodtype)
		return

	var/static/list/bloodtypes_safe = list(
		"A-" = list("A-", "O-"),
		"A+" = list("A-", "A+", "O-", "O+"),
		"B-" = list("B-", "O-"),
		"B+" = list("B-", "B+", "O-", "O+"),
		"AB-" = list("A-", "B-", "O-", "AB-"),
		"AB+" = list("A-", "A+", "B-", "B+", "O-", "O+", "AB-", "AB+"),
		"O-" = list("O-"),
		"O+" = list("O-", "O+"),
		"L" = list("L"),
		"U" = list("A-", "A+", "B-", "B+", "O-", "O+", "AB-", "AB+", "L", "U")
	)

	var/safe = bloodtypes_safe[bloodtype]
	if(safe)
		. = safe

//to add a splatter of blood or other mob liquid.
/mob/living/proc/add_splatter_floor(turf/T)
	if(!iscarbon(src))
		if(!HAS_TRAIT(src, TRAIT_SIMPLE_WOUNDS))
			return
	var/datum/blood_type/blood = get_blood_type()
	if(isnull(blood))
		return
	if(!T)
		T = get_turf(src)

	if(istype(T, /turf/open/water))
		var/turf/open/water/W = T
		if(!LAZYLEN(W.children))
			W.water_reagent = blood.reagent_type // this is dumb, but it works for now
			W.volume_status = WATER_VOLUME_NORMAL // no infinite vitae glitch
			W.water_volume = MINIMUM_WATER_VOLUME

		return
	var/obj/effect/decal/cleanable/blood/splatter/splatter = new /obj/effect/decal/cleanable/blood/splatter(T, blood.color)

	splatter.transfer_mob_blood_dna(src)
	splatter.update_appearance(UPDATE_ICON_STATE)
	T?.pollute_turf(/datum/pollutant/metallic_scent, 30)
	return TRUE

/mob/living/proc/add_drip_floor(turf/T, amt)
	if(!iscarbon(src))
		if(!HAS_TRAIT(src, TRAIT_SIMPLE_WOUNDS))
			return
	var/datum/blood_type/blood = get_blood_type()
	if(isnull(blood))
		return
	if(!T)
		T = get_turf(src)

	if(amt > 3)
		if(istype(T, /turf/open/water))
			var/turf/open/water/W = T
			W.water_reagent = blood.reagent_type // this is dumb, but it works for now
			W.volume_status = WATER_VOLUME_NORMAL // no infinite vitae glitch
			W.water_volume_maximum = MINIMUM_WATER_VOLUME
			W.water_volume = MINIMUM_WATER_VOLUME
			return

	playsound(src, 'sound/misc/bleed (3).ogg', 100, FALSE)

	var/obj/item/reagent_containers/container = locate(/obj/item/reagent_containers) in T
	if(container && container.is_open_container() && container.reagents.total_volume < container.reagents.maximum_volume)
		amt = amt - transfer_blood_impurities(container.reagents, amt, BLOODLETTING_MULT, src,  list(/datum/reagent/steam, /datum/reagent/water, /datum/reagent/blood, /datum/reagent/consumable/nutriment, /datum/reagent/consumable/soup))

	if(amt > 0.5)
		var/obj/effect/decal/cleanable/blood/puddle/P = locate() in T
		if(P)
			P.blood_vol += amt
			P.transfer_mob_blood_dna(src)
			P.update_appearance(UPDATE_ICON_STATE)
		else
			var/obj/effect/decal/cleanable/blood/drip/D = locate() in T
			if(D)
				D.blood_vol += amt
				D.drips++
				D.transfer_mob_blood_dna(src)
				D.update_appearance(UPDATE_ICON_STATE)
			else
				var/obj/effect/decal/cleanable/blood/drip/splatter = new /obj/effect/decal/cleanable/blood/drip(T, blood.color)
				splatter.transfer_mob_blood_dna(src)
				splatter.update_appearance(UPDATE_ICON_STATE)
	return TRUE

/mob/living/carbon/human/add_splatter_floor(turf/T, amt)
	if(!(NOBLOOD in dna.species.species_traits))
		. = ..()

/mob/living/carbon/human/add_splatter_floor(turf/T, small_drip)
	if(!(NOBLOOD in dna.species.species_traits))
		. = ..()
