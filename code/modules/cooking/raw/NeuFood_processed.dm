/* * * * * * * * * * * **
 *						*
 *		 NeuFood		*
 *	    (Preserves)		*
 *						*
 * * * * * * * * * * * **/

// -------------- FAT -----------------
/obj/item/reagent_containers/food/snacks/fat
	name = "fat"
	desc = ""
	icon_state = "fat"
	eat_effect = /datum/status_effect/debuff/uncookedfood
	possible_item_intents = list(/datum/intent/use)
	nutrition = FAT_NUTRITION
	item_weight = 230 GRAMS

/obj/item/reagent_containers/food/snacks/fat/interact_with_atom(atom/interacting_with, mob/living/user, list/modifiers)
	if(!isliving(interacting_with))
		return NONE

	if(interacting_with != user)
		return ..()

	if(user.zone_selected == BODY_ZONE_PRECISE_MOUTH)
		return ..()

	var/mob/living/M = interacting_with

	user.visible_message(
		span_warning("[user] starts to oil [user.p_them()]self up."),
		span_warning("I start oiling myself up."),
	)

	if(!do_after(user, 5 SECONDS, M))
		return ITEM_INTERACT_ANY_BLOCKER

	M.apply_status_effect(/datum/status_effect/buff/oiled)

	return ITEM_INTERACT_SUCCESS

/obj/item/reagent_containers/food/snacks/fat/item_interaction(mob/living/user, obj/item/tool, list/modifiers)
	if(!istype(tool, /obj/item/reagent_containers/glass))
		return ..()

	if(!isturf(loc) || !(locate(/obj/structure/table) in loc))
		to_chat(user, span_warning("You need to put [src] on a table to work on it."))
		return ITEM_INTERACT_BLOCKING

	if(!tool.reagents.has_reagent(/datum/reagent/consumable/sugar, 30))
		to_chat(user, span_notice("Needs more sugar to work it."))
		return ITEM_INTERACT_BLOCKING

	if(GET_MOB_SKILL_VALUE_OLD(user, /datum/attribute/skill/craft/cooking) <= 3) // cooks with less than 3 skill don´t know this recipe
		to_chat(user, span_warning("Gelatine is much too strange for you."))
		return ITEM_INTERACT_BLOCKING

	long_cooktime = (90 - ((GET_MOB_SKILL_VALUE_OLD(user, /datum/attribute/skill/craft/cooking)) * 15))
	to_chat(user, span_notice("Congealing the sugar..."))
	playsound(user, 'sound/foley/splishy.ogg', 100, TRUE, -1)
	if(do_after(user, long_cooktime, src))
		new /obj/item/reagent_containers/food/snacks/jellycake_base(loc)
		user.mind.add_sleep_experience(/datum/attribute/skill/craft/cooking/confectionery, (GET_MOB_ATTRIBUTE_VALUE(user, STAT_INTELLIGENCE)*0.5))
		qdel(src)
		tool.reagents.remove_reagent(/datum/reagent/consumable/sugar, 30)
		user.nobles_seen_servant_work()

	return ITEM_INTERACT_SUCCESS

// TALLOW is used as an intermediate crafting ingredient for other recipes.
/obj/item/reagent_containers/food/snacks/tallow
	name = "tallow"
	desc = "Fatty tissue is harvested from slain creachurs and rendered of its membraneous sinew to produce a hard shelf-stable \
	grease."
	icon_state = "tallow"
	tastes = list("grease" = 1, "oil" = 1, "regret" =1)
	obj_flags = CAN_BE_HIT
	nutrition = FAT_NUTRITION
	bitesize = 1
	dropshrink = 0.75
	item_weight = 270 GRAMS

/obj/item/reagent_containers/food/snacks/tallow/red
	name = "redtallow"
	desc = "Fatty tissue is harvested from slain creachurs and rendered of its membraneous sinew to produce a hard shelf-stable \
	grease. It has then been soaked in blood or something blood adjacent to make for an easily sourced and rather grim wax substitute. As they say in Grenzelhoft, one uses what one has."
	icon_state = "redtallow"
	tastes = list("grease" = 1, "oil" = 1, "regret" =1, "blood"=1,)

/obj/item/reagent_containers/food/snacks/tallow/attacked_by(obj/item/I, mob/living/user)
	. = ..()
	if(istype(I, /obj/item/inqarticles/indexer))
		var/obj/item/inqarticles/indexer/IND = I
		var/success
		if(HAS_TRAIT(user, TRAIT_INQUISITION))
			if(IND.full)
				if(tgui_alert(user, "SOAK THE TALLOW?", "IT'S JUST BLOOD", list("Yes", "No")) != "NO")
					success = TRUE
					IND.fullreset(user)
				else
					return
				if(success)
					changefood(/obj/item/reagent_containers/food/snacks/tallow/red, user)

// -------------- SPIDER HONEY -----------------
/obj/item/reagent_containers/food/snacks/spiderhoney
	name = "spider honey"
	icon_state = "spiderhoney"
	bitesize = 3
	nutrition = HONEY_NUTRITION
	w_class = WEIGHT_CLASS_TINY
	foodtype = SUGAR | RAW
	tastes = list("sweetness and spiderwebs" = 1)
	faretype = FARE_FINE

// -------------- TIEFLING SUGAR -----------------
/obj/item/reagent_containers/food/snacks/tiefsugar
	name = "Tiefling Sugar"
	desc ="Originating from subterra, Tiefling blood that has been expertly dried and mixed into a sugar base, sweetens when boiled."
	icon = 'icons/roguetown/items/produce.dmi'
	icon_state = "tsugar"
	tastes = list("sweet" = 1)
	nutrition = SUGAR_NUTRITION
	foodtype = SUGAR | RAW
	list_reagents = list(/datum/reagent/blood/tiefling = 11)
	item_weight = 150 GRAMS

// -------------- CHOCOLATE -----------------
/obj/item/reagent_containers/food/snacks/chocolate
	name = "chocolate bar"
	desc = "Unbelievably fancy chocolate, imported all the way from distant Grenzelhoft"
	icon_state = "chocolate"
	bitesize = 4
	slices_num = 3
	nutrition = CHOCCY_NUTRITION
	w_class = WEIGHT_CLASS_TINY
	tastes = list("rich sweetness" = 1)
	faretype = FARE_FINE
	rotprocess = null
	slice_path = /obj/item/reagent_containers/food/snacks/chocolate/chunk
	eat_effect = /datum/status_effect/buff/foodbuff
	foodtype = SUGAR | JUNKFOOD
	item_weight = 225 GRAMS //this is just the weight of the bakers chocolate bar I had in my pantry

/obj/item/reagent_containers/food/snacks/chocolate/chunk
	eat_effect = null
	slices_num = 0
	name = "chocolate chunk"
	icon_state = "chocolatechopped"
	nutrition = (CHOCCY_NUTRITION) / 3
	bitesize = 1
	tastes = list("chocolate" = 1)
	faretype = FARE_NEUTRAL
	foodtype = SUGAR | JUNKFOOD
	item_weight = 70 GRAMS


// -------------- SALUMOI (dwarven smoked sausage) -----------------
/obj/item/reagent_containers/food/snacks/meat/salami
	name = "salumoi"
	desc = "Traveling food invented by dwarves. Said to last for ten yils before spoiling"
	icon_state = "salumoi5"
	eat_effect = null
	slices_num = 4
	bitesize = 5
	slice_batch = FALSE
	nutrition = RAWMEAT_NUTRITION*DRIED_MOD
	slice_path = /obj/item/reagent_containers/food/snacks/meat/salami/slice
	tastes = list("salted meat" = 1)
	rotprocess = null
	slice_sound = TRUE
	faretype = FARE_POOR
	foodtype = MEAT
	item_weight = 325 GRAMS

/obj/item/reagent_containers/food/snacks/meat/salami/update_icon_state()
	if(slices_num)
		icon_state = "salumoi[slices_num]"
	else
		icon_state = "salumoi_slice"
	return ..()

/obj/item/reagent_containers/food/snacks/meat/salami/on_consume(mob/living/eater)
	..()
	if(slices_num)
		switch(bitecount)
			if(1)
				slices_num = 4
			if(2)
				slices_num = 3
			if(3)
				slices_num = 2
			if(4)
				changefood(slice_path, eater)

/obj/item/reagent_containers/food/snacks/meat/salami/slice
	eat_effect = null
	slices_num = 0
	name = "salumoi"
	icon_state = "salumoi_slice"
	nutrition = (RAWMEAT_NUTRITION*DRIED_MOD) / 5
	bitesize = 1
	tastes = list("salted meat" = 1)
	faretype = FARE_NEUTRAL
	foodtype = MEAT
	item_weight = 55 GRAMS

// -------------- COPPIETTE (dried meat) -----------------
/obj/item/reagent_containers/food/snacks/cooked/coppiette
	name = "coppiette"
	desc = "Dried meat sticks."
	icon_state = "coppiette"
	base_icon_state = "coppiette"
	biting = TRUE
	bitesize = 5
	tastes = list("salted meat" = 1)
	rotprocess = null
	nutrition = RAWMEAT_NUTRITION*DRIED_MOD
	faretype = FARE_POOR
	foodtype = MEAT
	item_weight = 175 GRAMS


// -------------- SALTFISH -----------------
/obj/item/reagent_containers/food/snacks/saltfish
	name = "saltfish"
	desc = "Dried fish."
	icon = 'icons/roguetown/misc/fish.dmi'
	icon_state = "clownfishdried"
	eat_effect = null
	bitesize = 4
	slice_path = null
	tastes = list("salted meat" = 1)
	rotprocess = null
	nutrition = RAWMEAT_NUTRITION*DRIED_MOD
	dropshrink = 0.6
	faretype = FARE_POOR
	foodtype = MEAT
	item_weight = 175 GRAMS

/obj/item/reagent_containers/food/snacks/saltfish/CheckParts(list/parts_list)
	for(var/obj/item/reagent_containers/food/snacks/M in parts_list)
		icon_state = "[initial(M.icon_state)]dried"
		qdel(M)

// -------------- SALO (salted fat) -----------------
/obj/item/reagent_containers/food/snacks/fat/salo
	name = "salo"
	icon_state = "salo4"
	bitesize = 4
	nutrition = FAT_NUTRITION*2*DRIED_MOD
	slice_path = /obj/item/reagent_containers/food/snacks/fat/salo/slice
	slices_num = 4
	slice_batch = FALSE
	slice_sound = TRUE
	eat_effect = null
	faretype = FARE_POOR
	foodtype = MEAT

/obj/item/reagent_containers/food/snacks/fat/salo/update_icon_state()
	if(slices_num)
		icon_state = "salo[slices_num]"
	else
		icon_state = "saloslice"
	return ..()

/obj/item/reagent_containers/food/snacks/fat/salo/on_consume(mob/living/eater)
	..()
	if(slices_num)
		if(bitecount == 1)
			slices_num = 3
		if(bitecount == 2)
			slices_num = 2
		if(bitecount == 3)
			changefood(slice_path, eater)

/obj/item/reagent_containers/food/snacks/fat/salo/slice
	name = "salo"
	icon_state = "saloslice"
	bitesize = 2
	slices_num = FALSE
	slice_path = FALSE
	nutrition = (FAT_NUTRITION*2*DRIED_MOD) * 0.25
	foodtype = MEAT
	item_weight = 30 GRAMS

/*------------\
| Dried Fruit |
\------------*/

// -------------- RAISINS -----------------
/obj/item/reagent_containers/food/snacks/raisins
	name = "raisins"
	icon = 'icons/roguetown/items/produce.dmi'
	icon_state = "raisins"
	base_icon_state = "raisins"
	biting = TRUE
	bitesize = 5
	nutrition = RAISIN_NUTRITION
	w_class = WEIGHT_CLASS_TINY
	tastes = list("dried fruit" = 1)
	foodtype = FRUIT
	faretype = FARE_POOR
	item_weight = 5 GRAMS

/obj/item/reagent_containers/food/snacks/raisins/CheckParts(list/parts_list)
	..()
	for(var/obj/item/reagent_containers/food/snacks/M in parts_list)
		color = M.filling_color
		if(M.reagents)
			M.reagents.remove_reagent(/datum/reagent/consumable/nutriment, M.reagents.total_volume)
			M.reagents.trans_to(src, M.reagents.total_volume)
		qdel(M)

/obj/item/reagent_containers/food/snacks/raisins/poison
	list_reagents = list(/datum/reagent/berrypoison = 5)

// -------------- STRAWBERRY -----------------

/obj/item/reagent_containers/food/snacks/strawberry_dried
	name = "dried strawberry"
	icon_state = "driedstrawberry"
	dropshrink = 0.8
	bitesize = 3
	w_class = WEIGHT_CLASS_TINY
	tastes = list("dried fruit" = 1)
	foodtype = FRUIT
	faretype = FARE_NEUTRAL
	nutrition = DRIEDFRUIT_NUTRITION
	item_weight = 6 GRAMS

// -------------- TANGERINE -----------------

/obj/item/reagent_containers/food/snacks/tangerine_dried
	name = "dried tangerine"
	icon_state = "driedtangerine"
	dropshrink = 0.8
	bitesize = 3
	w_class = WEIGHT_CLASS_TINY
	tastes = list("dried fruit" = 1)
	foodtype = FRUIT
	faretype = FARE_NEUTRAL
	nutrition = DRIEDFRUIT_NUTRITION
	item_weight = 44 GRAMS

// -------------- PLUM -----------------

/obj/item/reagent_containers/food/snacks/plum_dried
	name = "dried plum"
	icon_state = "driedplum"
	dropshrink = 0.8
	bitesize = 3
	w_class = WEIGHT_CLASS_TINY
	tastes = list("dried fruit" = 1)
	foodtype = FRUIT
	faretype = FARE_NEUTRAL
	nutrition = DRIEDFRUIT_NUTRITION
	item_weight = 33 GRAMS

// -------------- APPLE -----------------

/obj/item/reagent_containers/food/snacks/apple_dried
	name = "dried apple"
	icon_state = "driedapple"
	dropshrink = 0.8
	bitesize = 3
	w_class = WEIGHT_CLASS_TINY
	tastes = list("dried fruit" = 1)
	foodtype = FRUIT
	faretype = FARE_NEUTRAL
	nutrition = DRIEDFRUIT_NUTRITION
	item_weight = 91 GRAMS

// -------------- PEAR -----------------

/obj/item/reagent_containers/food/snacks/pear_dried
	name = "dried pear"
	icon_state = "driedpear"
	dropshrink = 0.8
	bitesize = 3
	w_class = WEIGHT_CLASS_TINY
	tastes = list("dried fruit" = 1)
	foodtype = FRUIT
	faretype = FARE_NEUTRAL
	nutrition = DRIEDFRUIT_NUTRITION
	item_weight = 86 GRAMS

/***************** Mushrooms *****************/

/obj/item/reagent_containers/food/snacks/waddle_dried
	name = "dried waddle"
	desc = "A waddle mushroom that has been dried for use in tea. Not pleasant to eat in this state."
	icon_state = "driedwaddle"
	dropshrink = 1
	bitesize = 3
	w_class = WEIGHT_CLASS_TINY
	tastes = list("woody" = 1,"bitter" = 1)
	foodtype = MUSHROOM
	faretype = FARE_POOR
	eat_effect = /datum/status_effect/debuff/uncookedfood
	nutrition = VEGGIE_NUTRITION*DRIED_MOD
	item_weight = 7 GRAMS

/*------------\
| Salted milk |
\------------*/		// The base for making butter and cheese

/datum/reagent/consumable/milk/gote
	name = "gote milk"
	taste_description = "gote milk"

/datum/reagent/consumable/milk/salted_gote
	name = "salted gote milk"
	taste_description = "salty gote-milk"

/datum/reagent/consumable/milk/salted
	name = "salted milk"
	taste_description = "salty milk"

/*-------\
| Butter |
\-------*/

/*	............   Churning butter   ................ */
/obj/item/reagent_containers/glass/bucket/wooden/item_interaction(mob/living/user, obj/item/tool, list/modifiers)
	if(istype(tool, /obj/item/kitchen/spoon))
		long_cooktime = (200 - ((GET_MOB_SKILL_VALUE_OLD(user, /datum/attribute/skill/craft/cooking))*22))
		if(!reagents.has_reagent(/datum/reagent/consumable/milk/salted, 15) && !reagents.has_reagent(/datum/reagent/consumable/milk/salted_gote, 15))
			to_chat(user, span_warning("Not enough salted milk."))
			return ITEM_INTERACT_BLOCKING

		user.adjust_stamina(40) // forgot stamina is our lovely stamloss proc here
		user.visible_message("<span class='info'>[user] churns butter...</span>")
		playsound(user, 'sound/foley/butterchurn.ogg', 100, TRUE, -1)
		if(do_after(user, long_cooktime, src))
			user.adjust_stamina(50)
			if(reagents.has_reagent(/datum/reagent/consumable/milk/salted, 15))
				reagents.remove_reagent(/datum/reagent/consumable/milk/salted, 15)
			if(reagents.has_reagent(/datum/reagent/consumable/milk/salted_gote, 15))
				reagents.remove_reagent(/datum/reagent/consumable/milk/salted_gote, 15)
			new /obj/item/reagent_containers/food/snacks/butter(drop_location())
			user.mind.add_sleep_experience(/datum/attribute/skill/craft/cooking/cheesemaking, (GET_MOB_ATTRIBUTE_VALUE(user, STAT_INTELLIGENCE)))
			user.nobles_seen_servant_work()
		return ITEM_INTERACT_SUCCESS

	if(istype(tool, /obj/item/natural/cloth) && (user.used_intent.type == INTENT_USE || user.used_intent.type == INTENT_SOAK))
		long_cooktime = (100 - ((GET_MOB_SKILL_VALUE_OLD(user, /datum/attribute/skill/craft/cooking)) * 12))
		var/milk = null
		var/cheese = null
		if(reagents.has_reagent(/datum/reagent/consumable/milk/salted, 5))
			milk = /datum/reagent/consumable/milk/salted
			cheese = /obj/item/reagent_containers/food/snacks/cheese
		if(reagents.has_reagent(/datum/reagent/consumable/milk/salted_gote, 5))
			milk = /datum/reagent/consumable/milk/salted_gote
			cheese = /obj/item/reagent_containers/food/snacks/cheese
		if(milk)
			if(tool.reagents.total_volume > 0)
				to_chat(user, span_warning("The [tool.name] is still soaked with something."))
			else
				user.visible_message("<span class='info'>[user] strains fresh cheese...</span>")
				playsound(src, pick('sound/foley/waterwash (1).ogg','sound/foley/waterwash (2).ogg'), 100, FALSE)
				if(do_after(user, long_cooktime, src))
					reagents.remove_reagent(milk, 5)
					new cheese(drop_location())
					user.mind.add_sleep_experience(/datum/attribute/skill/craft/cooking/cheesemaking, (GET_MOB_ATTRIBUTE_VALUE(user, STAT_INTELLIGENCE)))
				user.nobles_seen_servant_work()
			return ITEM_INTERACT_SUCCESS

	return ..()

// -------------- BUTTER -----------------
/obj/item/reagent_containers/food/snacks/butter
	name = "stick of butter"
	desc = ""
	icon_state = "butter6"
	nutrition = BUTTER_NUTRITION
	foodtype = DAIRY
	eat_effect = /datum/status_effect/debuff/uncookedfood
	slice_path = /obj/item/reagent_containers/food/snacks/butterslice
	slices_num = 6
	slice_batch = FALSE
	bitesize = 6
	slice_sound = TRUE
	tastes = list("butter" = 1)
	faretype = FARE_IMPOVERISHED
	item_weight = 150 GRAMS

/obj/item/reagent_containers/food/snacks/butter/update_icon_state()
	if(slices_num)
		icon_state = "butter[slices_num]"
	else
		icon_state = "butter_slice"
	return ..()

/obj/item/reagent_containers/food/snacks/butter/on_consume(mob/living/eater)
	..()
	if(slices_num)
		switch(bitecount)
			if(1)
				slices_num = 5
			if(2)
				slices_num = 4
			if(3)
				slices_num = 3
			if(4)
				slices_num = 2
			if(5)
				changefood(slice_path, eater)

/obj/item/reagent_containers/food/snacks/butterslice
	icon_state = "butter_slice"
	name = "butter"
	foodtype = DAIRY
	eat_effect = /datum/status_effect/debuff/uncookedfood
	nutrition = BUTTER_NUTRITION * SLICED_MOD
	tastes = list("butter" = 1)
	bitesize = 1
	faretype = FARE_IMPOVERISHED
	item_weight = 25 GRAMS

/*	............   Pestran Stick   ................ */

/obj/item/reagent_containers/food/snacks/pestranstick
	name = "pestran stick"
	desc = "An unappetizing snack adored by devout Pestrans, somehow doesn't taste half bad."
	icon_state = "pestranstick"
	nutrition = BUTTER_NUTRITION
	tastes = list("raw unsalted butter on a stick" = 1)
	trash = /obj/item/grown/log/tree/stick
	foodtype = DAIRY
	bitesize = 3
	faretype = FARE_POOR
	item_weight = 240 GRAMS

// -------------- CHEESE -----------------
/obj/item/reagent_containers/food/snacks/cheese
	name = "fresh cheese"
	icon_state = "freshcheese"
	bitesize = 1
	nutrition = CHEESE_NUTRITION
	w_class = WEIGHT_CLASS_TINY
	tastes = list("cheese" = 1)
	foodtype = DAIRY
	eat_effect = null
	rotprocess = SHELFLIFE_DECENT
	become_rot_type = null
	slice_path = null
	faretype = FARE_POOR
	item_weight = 224 GRAMS

/obj/item/reagent_containers/food/snacks/cheese/interact_with_atom(atom/interacting_with, mob/living/user, list/modifiers)
	if(!istype(interacting_with, /obj/item/natural/cloth))
		return ..()

	if(!isturf(interacting_with.loc) || !(locate(/obj/structure/table) in interacting_with.loc))
		to_chat(user, span_warning("You need to put [interacting_with] on a table to work on it."))
		return ITEM_INTERACT_BLOCKING

	user.visible_message("<span class='info'>[user] starts packing the cloth with fresh cheese...</span>")

	playsound(user, 'sound/foley/dropsound/food_drop.ogg', 30, TRUE, -1)
	if(do_after(user, 3 SECONDS, interacting_with))
		new /obj/item/reagent_containers/food/snacks/foodbase/cheesewheel_start(interacting_with.loc)
		user.mind.add_sleep_experience(/datum/attribute/skill/craft/cooking/cheesemaking, (GET_MOB_ATTRIBUTE_VALUE(user, STAT_INTELLIGENCE)*0.5))
		qdel(interacting_with)
		qdel(src)
		user.nobles_seen_servant_work()
	return ITEM_INTERACT_SUCCESS

/obj/item/reagent_containers/food/snacks/foodbase/cheesewheel_start
	name = "unfinished cheese wheel"
	icon_state = "cheesewheel_1"
	w_class = WEIGHT_CLASS_BULKY
	do_random_pixel_offset = FALSE
	grid_height = 32
	grid_width = 96
	item_weight = 2.2 KILOGRAMS

	var/cheese_added = 0

/obj/item/reagent_containers/food/snacks/foodbase/cheesewheel_start/item_interaction(mob/living/user, obj/item/tool, list/modifiers)
	if(!istype(tool, /obj/item/reagent_containers/food/snacks/cheese))
		return ..()

	short_cooktime = (50 - ((GET_MOB_SKILL_VALUE_OLD(user, /datum/attribute/skill/craft/cooking)) * 8))

	if(!isturf(loc) || !(locate(/obj/structure/table) in (loc)))
		to_chat(user, span_warning("You need to put [src] on a table to work on it."))
		return ITEM_INTERACT_BLOCKING

	if(cheese_added >= 3)
		to_chat(user, span_warning("The cheese is maturing!"))
		return ITEM_INTERACT_BLOCKING

	playsound(user, 'sound/foley/dropsound/food_drop.ogg', 30, TRUE, -1)

	if(do_after(user, short_cooktime, src))
		item_weight += 0.2 KILOGRAMS
		cheese_added++
		if(cheese_added == 3)
			addtimer(CALLBACK(src, PROC_REF(maturing_done)), 5 MINUTES)
		qdel(tool)
		update_appearance(UPDATE_ICON_STATE | UPDATE_NAME | UPDATE_DESC)

	return ITEM_INTERACT_SUCCESS

/obj/item/reagent_containers/food/snacks/foodbase/cheesewheel_start/proc/maturing_done()
	if(QDELETED(src))
		return

	playsound(src, 'sound/foley/rustle2.ogg', 100, TRUE, -1)
	new /obj/item/reagent_containers/food/snacks/cheddar(get_turf(src))
	new /obj/item/natural/cloth(get_turf(src))
	qdel(src)

/obj/item/reagent_containers/food/snacks/foodbase/cheesewheel_start/update_icon_state()
	. = ..()

	if(cheese_added == 3)
		icon_state = "cheesewheel_end"
	else if(cheese_added)
		icon_state = "cheesewheel_[cheese_added + 1]"

/obj/item/reagent_containers/food/snacks/foodbase/cheesewheel_start/update_name(updates)
	. = ..()
	if(cheese_added == 3)
		name = "maturing cheese wheel"

/obj/item/reagent_containers/food/snacks/foodbase/cheesewheel_start/update_desc(updates)
	. = ..()
	if(cheese_added == 3)
		desc = desc = "Slowly solidifying, best left alone a bit longer."

/obj/item/reagent_containers/food/snacks/cheese/gote
	name = "fresh gote cheese"

/obj/item/reagent_containers/food/snacks/cheddar
	name = "wheel of cheese"
	icon_state = "cheesewheel"
	dropshrink = 0.8
	bitesize = 6
	nutrition = CHEESE_NUTRITION/SLICED_MOD
	w_class = WEIGHT_CLASS_NORMAL
	tastes = list("cheese" = 1)
	eat_effect = null
	rotprocess = SHELFLIFE_LONG
	slices_num = 6
	slice_batch = TRUE
	slice_path = /obj/item/reagent_containers/food/snacks/cheese_wedge
	become_rot_type = /obj/item/reagent_containers/food/snacks/cheddar/aged
	slice_sound = TRUE
	grid_height = 32
	grid_width = 96
	foodtype = DAIRY
	item_weight = 3 KILOGRAMS

/obj/item/reagent_containers/food/snacks/cheddar/aged
	name = "wheel of aged cheese"
	icon_state = "blue_cheese"
	slice_path = /obj/item/reagent_containers/food/snacks/cheese_wedge/aged
	become_rot_type = null
	rotprocess = null
	sellprice = 60
	faretype = FARE_FINE
	item_weight = 3 KILOGRAMS

/obj/item/reagent_containers/food/snacks/cheese_wedge
	name = "wedge of cheese"
	icon_state = "cheese_wedge"
	dropshrink = 0.8
	nutrition = CHEESE_NUTRITION
	w_class = WEIGHT_CLASS_TINY
	foodtype = DAIRY
	tastes = list("cheese" = 1)
	rotprocess = SHELFLIFE_LONG
	slice_batch = TRUE
	slice_path = /obj/item/reagent_containers/food/snacks/cheddarslice
	slices_num = 3
	become_rot_type = /obj/item/reagent_containers/food/snacks/cheese_wedge/aged
	baitpenalty = 0
	isbait = TRUE
	fishloot = list(/obj/item/reagent_containers/food/snacks/fish/carp = 10,
					/obj/item/reagent_containers/food/snacks/fish/eel = 5,
					/obj/item/reagent_containers/food/snacks/fish/angler = 1,
					/obj/item/reagent_containers/food/snacks/fish/shrimp = 3)
	item_weight = 500 GRAMS

/obj/item/reagent_containers/food/snacks/cheese_wedge/aged
	name = "wedge of aged cheese"
	icon_state = "blue_cheese_wedge"
	slice_path = /obj/item/reagent_containers/food/snacks/cheddarslice/aged
	become_rot_type = null
	rotprocess = null
	sellprice = 10
	faretype = FARE_FINE

/obj/item/reagent_containers/food/snacks/cheddarslice
	name = "slice of cheese"
	icon_state = "cheese_slice"
	bitesize = 1
	dropshrink = 0.8
	nutrition = CHEESE_NUTRITION
	foodtype = DAIRY
	w_class = WEIGHT_CLASS_TINY
	tastes = list("cheese" = 1)
	eat_effect = null
	rotprocess = SHELFLIFE_SHORT
	slices_num = null
	slice_path = null
	become_rot_type = null
	baitpenalty = 0
	isbait = TRUE
	fishloot = list(/obj/item/reagent_containers/food/snacks/fish/carp = 10,
					/obj/item/reagent_containers/food/snacks/fish/eel = 5,
					/obj/item/reagent_containers/food/snacks/fish/shrimp = 3)
	faretype = FARE_FINE
	item_weight = 155 GRAMS

/obj/item/reagent_containers/food/snacks/cheddarslice/aged
	name = "slice of aged cheese"
	icon_state = "blue_cheese_slice"
	become_rot_type = null
	rotprocess = null
	faretype = FARE_FINE

/*--------\
| Gelatine |
\--------*/

// -------------- Gelatine Base -----------------

/obj/item/reagent_containers/food/snacks/jellycake_base
	name = "plain gelatie cake"
	desc = "A mildly unappetising desert, fittingly considered a delicacy by orcs. Though it is traditionally made plain, chefs often mercifully flavor it with fruit."
	icon_state = "basegelatinecake"
	dropshrink = 0.8
	slice_path = /obj/item/reagent_containers/food/snacks/jellyslice_base
	slices_num = 4
	nutrition = (SUGAR_NUTRITION*2 + FAT_NUTRITION) * COOK_MOD
	w_class = WEIGHT_CLASS_NORMAL
	tastes = list("unflavored gelatine" = 1)
	foodtype = MEAT | SUGAR | GROSS
	slice_batch = TRUE
	slice_sound = TRUE
	eat_effect = /datum/status_effect/debuff/uncookedfood
	rotprocess = null
	bitesize = 4
	faretype = FARE_POOR
	item_weight = 950 GRAMS

/obj/item/reagent_containers/food/snacks/jellyslice_base
	name = "plain gelatine slice"
	icon_state = "basegelatinslice"
	dropshrink = 0.8
	slices_num = 0
	bitesize = 2
	tastes = list("unflavored gelatine" = 1)
	eat_effect = /datum/status_effect/debuff/uncookedfood
	w_class = WEIGHT_CLASS_NORMAL
	foodtype = MEAT | SUGAR | GROSS
	rotprocess = null
	faretype = FARE_POOR
	nutrition = (SUGAR_NUTRITION*2 + FAT_NUTRITION) * 0.25
	item_weight = 950 GRAMS


// -------------- Apple Gelatine -----------------

/obj/item/reagent_containers/food/snacks/jellycake_apple
	name = "apple gelatine cake"
	desc = "A mildly unappetising desert, fittingly considered a delicacy by orcs. This one is colored blood(apple)-red."
	icon_state = "applegelatinecake"
	dropshrink = 0.8
	slice_path = /obj/item/reagent_containers/food/snacks/jellyslice_apple
	slices_num = 4
	nutrition = (SUGAR_NUTRITION*2 + FAT_NUTRITION + FRUIT_NUTRITION) * COOK_MOD
	w_class = WEIGHT_CLASS_NORMAL
	tastes = list("sweet gelatine" = 1, "sweet apple"  = 1)
	foodtype = MEAT | SUGAR | FRUIT
	slice_batch = TRUE
	slice_sound = TRUE
	eat_effect = /datum/status_effect/buff/foodbuff
	rotprocess = null
	bitesize = 4
	faretype = FARE_FINE
	item_weight = 1 KILOGRAMS

/obj/item/reagent_containers/food/snacks/jellyslice_apple
	name = "apple gelatine slice"
	icon_state = "applegelatineslice"
	dropshrink = 0.8
	slices_num = 0
	bitesize = 2
	nutrition = (SUGAR_NUTRITION*2 + FAT_NUTRITION + FRUIT_NUTRITION) * COOK_MOD * 0.25
	tastes = list("sweet gelatine" = 1, "sweet apple" = 1)
	w_class = WEIGHT_CLASS_NORMAL
	foodtype = MEAT | SUGAR | FRUIT
	rotprocess = null
	faretype = FARE_FINE
	item_weight = 250 GRAMS

// -------------- Tangeringe Gelatine -----------------

/obj/item/reagent_containers/food/snacks/jellycake_tangerine
	name = "tangerine gelatine cake"
	desc = "A mildly unappetising desert, fittingly considered a delicacy by orcs. This one is bittersweet, like the triumph of battle."
	icon_state = "tangerinegelatinecake"
	dropshrink = 0.8
	slice_path = /obj/item/reagent_containers/food/snacks/jellyslice_tangerine
	slices_num = 4
	nutrition = (SUGAR_NUTRITION*2 + FAT_NUTRITION + FRUIT_NUTRITION) * COOK_MOD
	w_class = WEIGHT_CLASS_NORMAL
	tastes = list("sweet gelatine" = 1, "sweet tangerine" = 1)
	foodtype = MEAT | SUGAR | FRUIT
	slice_batch = TRUE
	slice_sound = TRUE
	eat_effect = /datum/status_effect/buff/foodbuff
	rotprocess = null
	bitesize = 4
	faretype = FARE_FINE
	item_weight = 1 KILOGRAMS

/obj/item/reagent_containers/food/snacks/jellyslice_tangerine
	name = "tangerine gelatine slice"
	icon_state = "tangerinegelatineslice"
	dropshrink = 0.8
	slices_num = 0
	bitesize = 2
	nutrition = (SUGAR_NUTRITION*2 + FAT_NUTRITION + FRUIT_NUTRITION) * COOK_MOD * 0.25
	tastes = list("sweet gelatine" = 1, "sweet tangerine")
	w_class = WEIGHT_CLASS_NORMAL
	foodtype = MEAT | SUGAR | FRUIT
	rotprocess = null
	faretype = FARE_FINE
	item_weight = 250 GRAMS

// -------------- Plum Gelatine -----------------

/obj/item/reagent_containers/food/snacks/jellycake_plum
	name = "plum gelatine cake"
	desc = "A mildly unappetising desert, fittingly considered a delicacy by orcs. Like the plum this treat is made from, orcs persevere against all."
	icon_state = "plumgelatinecake"
	dropshrink = 0.8
	slice_path = /obj/item/reagent_containers/food/snacks/jellyslice_plum
	slices_num = 4
	nutrition = (SUGAR_NUTRITION*2 + FAT_NUTRITION + FRUIT_NUTRITION) * COOK_MOD
	w_class = WEIGHT_CLASS_NORMAL
	tastes = list("sweet gelatine" = 1, "sweet plum" = 1)
	foodtype = MEAT | SUGAR | FRUIT
	slice_batch = TRUE
	slice_sound = TRUE
	eat_effect = /datum/status_effect/buff/foodbuff
	rotprocess = null
	bitesize = 4
	faretype = FARE_FINE
	item_weight = 1 KILOGRAMS

/obj/item/reagent_containers/food/snacks/jellyslice_plum
	name = "plum gelatine slice"
	icon_state = "plumgelatineslice"
	dropshrink = 0.8
	slices_num = 0
	bitesize = 2
	nutrition = (SUGAR_NUTRITION*2 + FAT_NUTRITION + FRUIT_NUTRITION) * COOK_MOD * 0.25
	tastes = list("sweet gelatine" = 1, "sweet plum")
	w_class = WEIGHT_CLASS_NORMAL
	foodtype = MEAT | SUGAR | FRUIT
	rotprocess = null
	faretype = FARE_FINE
	item_weight = 250 GRAMS


// -------------- Lime Gelatine -----------------

/obj/item/reagent_containers/food/snacks/jellycake_lime
	name = "lime gelatine cake"
	desc = "A mildly unappetising desert, fittingly considered a delicacy by orcs. This one is green, naturally."
	icon_state = "limegelatinecake"
	dropshrink = 0.8
	slice_path = /obj/item/reagent_containers/food/snacks/jellyslice_lime
	slices_num = 4
	nutrition = (SUGAR_NUTRITION*2 + FAT_NUTRITION + FRUIT_NUTRITION) * COOK_MOD
	w_class = WEIGHT_CLASS_NORMAL
	tastes = list("sweet gelatine" = 1, "sweet lime" = 1)
	foodtype = MEAT | SUGAR | FRUIT
	slice_batch = TRUE
	slice_sound = TRUE
	eat_effect = /datum/status_effect/buff/foodbuff
	rotprocess = null
	bitesize = 4
	faretype = FARE_FINE
	item_weight = 1 KILOGRAMS

/obj/item/reagent_containers/food/snacks/jellyslice_lime
	name = "lime gelatine slice"
	icon_state = "limegelatineslice"
	dropshrink = 0.8
	slices_num = 0
	bitesize = 2
	nutrition = (SUGAR_NUTRITION*2 + FAT_NUTRITION + FRUIT_NUTRITION) * COOK_MOD * 0.25
	tastes = list("sweet gelatine" = 1, "sweet lime")
	w_class = WEIGHT_CLASS_NORMAL
	foodtype = MEAT | SUGAR | FRUIT
	rotprocess = null
	faretype = FARE_FINE
	item_weight = 250 GRAMS

// -------------- Pear Gelatine -----------------

/obj/item/reagent_containers/food/snacks/jellycake_pear
	name = "pear gelatine cake"
	desc = "A mildly unappetising dessert, fittingly considered a delicacy by orcs. This flavor is a strange fusion of Zalad and Orcish cuisines."
	icon_state = "peargelatinecake"
	dropshrink = 0.8
	slice_path = /obj/item/reagent_containers/food/snacks/jellyslice_pear
	slices_num = 4
	nutrition = (SUGAR_NUTRITION*2 + FAT_NUTRITION + FRUIT_NUTRITION) * COOK_MOD
	w_class = WEIGHT_CLASS_NORMAL
	tastes = list("sweet gelatine" = 1, "sweet pear" = 1)
	foodtype = MEAT | SUGAR | FRUIT
	slice_batch = TRUE
	slice_sound = TRUE
	eat_effect = /datum/status_effect/buff/foodbuff
	rotprocess = null
	bitesize = 4
	faretype = FARE_FINE
	item_weight = 1 KILOGRAMS

/obj/item/reagent_containers/food/snacks/jellyslice_pear
	name = "pear gelatine slice"
	icon_state = "peargelatineslice"
	dropshrink = 0.8
	slices_num = 0
	bitesize = 2
	nutrition = (SUGAR_NUTRITION*2 + FAT_NUTRITION + FRUIT_NUTRITION) * COOK_MOD * 0.25
	tastes = list("sweet gelatine" = 1, "sweet pear")
	w_class = WEIGHT_CLASS_NORMAL
	foodtype = MEAT | SUGAR | FRUIT
	rotprocess = null
	faretype = FARE_FINE
	item_weight = 250 GRAMS
