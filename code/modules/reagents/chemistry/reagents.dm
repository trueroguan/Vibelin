GLOBAL_LIST_INIT(name2reagent, build_name2reagent())

/proc/build_name2reagent()
	. = list()
	for (var/t in subtypesof(/datum/reagent))
		var/datum/reagent/R = t
		if (length(initial(R.name)))
			.[ckey(initial(R.name))] = t

//Various reagents
//Toxin & acid reagents
//Hydroponics stuff

/datum/reagent
	var/name = "Reagent"
	var/description = ""
	var/specific_heat = SPECIFIC_HEAT_DEFAULT		//J/(K*mol)
	var/taste_description = ""
	var/scent_description = ""
	var/taste_mult = 1 //how this taste compares to others. Higher values means it is more noticable
	var/glass_name = "glass of ...what?" // use for specialty drinks.
	var/glass_desc = ""
	var/glass_icon_state = null // Otherwise just sets the icon to a normal glass with the mixture of the reagents in the glass.
	var/shot_glass_icon_state = null
	var/datum/reagents/holder = null
	var/reagent_state = LIQUID
	/// Special data associated with the reagent that will be passed on upon transfer to a new holder. KEEP UNINITIALIZED IF POSSIBLE.
	var/list/data
	///A list of causes why this chem should skip being removed, if the length is 0 it will be removed from holder naturally, if this is >0 it will not be removed from the holder.
	var/list/reagent_removal_skip_list = list()
	var/current_cycle = 0
	var/volume = 0									//pretend this is moles
	var/color = "#000000" // rgb: 0, 0, 0
	var/random_reagent_color = FALSE
	var/alpha = 255
	var/can_synth = TRUE // can this reagent be synthesized? (for example: odysseus syringe gun)
	/// How fast the reagent is metabolized by the mob
	var/metabolization_rate = REAGENTS_METABOLISM
	var/overrides_metab = 0
	var/overdose_threshold = 0
	var/addiction_threshold = 0
	var/addiction_stage = 0
	var/overdosed = 0 // You fucked up and this is now triggering its overdose effects, purge that shit quick.
	var/self_consuming = FALSE
	var/reagent_weight = 1 //affects how far it travels when sprayed
	var/metabolizing = FALSE
	var/harmful = FALSE //is it bad for you? Currently only used for borghypo. C2s and Toxins have it TRUE by default.
	///The set of exposure methods this penetrates skin with.
	var/penetrates_skin = VAPOR
	var/evaporates = TRUE
	///How much fire power does the liquid have, for burning on simulated liquids. Not enough fire power/unit of entire mixture may result in no fire
	var/liquid_fire_power = 0
	///How fast does the liquid burn on simulated turfs, if it does
	var/liquid_fire_burnrate = 0
	///Whether a fire from this requires oxygen in the atmosphere
	var/fire_needs_oxygen = TRUE
	///The opacity of the chems used to determine the alpha of liquid turfs
	var/opacity = 175
	///The rate of evaporation in units per call
	var/evaporation_rate = 2
	/// do we have a turf exposure (used to prevent liquids doing un-needed processes)
	var/turf_exposure = FALSE
	/// are we slippery?
	var/slippery = TRUE
	///do we glow?
	var/glows = FALSE
	/// Base quality for newly created reagents of this type
	var/base_recipe_quality = COOK_QUALITY_NORMAL
	var/dead_head = TRUE
	///if we are false we don't apply the liver efficiency to our metabolization
	var/liver_chemical = TRUE
	/// Boiling point in Kelvin. Used by chem_separator to determine distillation order.
	var/boiling_point = T0C + 100
	/// A list of traits to apply while the reagent is being metabolized.
	var/list/metabolized_traits
	/// A list of traits to apply while the reagent is in a mob.
	var/list/added_traits
	/// Our price per ligaue in mammons
	var/price_per_unit = 0

/datum/reagent/Destroy() // This should only be called by the holder, so it's already handled clearing its references
	. = ..()
	holder = null

/// Applies this reagent to a [/mob/living]
/datum/reagent/proc/expose_mob(mob/living/exposed_mob, methods = TOUCH, reac_volume, show_message = TRUE, touch_protection = 0)
	SHOULD_CALL_PARENT(TRUE)

	if(SEND_SIGNAL(src, COMSIG_REAGENT_EXPOSE_MOB, exposed_mob, methods, reac_volume, show_message, touch_protection) & COMPONENT_NO_EXPOSE_REAGENTS)
		return

	if(isnull(exposed_mob.reagents)) // lots of simple mobs do not have a reagents holder
		return

	if(penetrates_skin & methods) //smoke, foam, spray
		var/amount = round(reac_volume*clamp((1 - touch_protection), 0, 1), 0.1)
		if(amount >= 0.5)
			exposed_mob.reagents.add_reagent(type, amount)

/datum/reagent/proc/reaction_obj(obj/O, volume)
	return

/datum/reagent/proc/evaporate(turf/exposed_turf, reac_volume)
	return

/datum/reagent/proc/reaction_turf(turf/T, volume)
	return

/// Return TRUE if reagent should be transfered to affected_mob when absorbed through a bodypart
/datum/reagent/proc/on_bodypart_absorb(mob/living/carbon/affected_mob, obj/item/bodypart/affected_bodypart, amount_to_transfer)
	return TRUE

/datum/reagent/proc/on_mob_life(mob/living/carbon/M, efficiency)
	SHOULD_CALL_PARENT(TRUE)
	current_cycle++
	if(holder)
		var/adjusted_metabolization_rate = metabolization_rate
		if(istype(src, /datum/reagent/consumable/ethanol) && has_world_trait(/datum/world_trait/baotha_revelry))
			adjusted_metabolization_rate = adjusted_metabolization_rate * (is_ascendant(BAOTHA) ? 0.33 : 0.5)

		holder.remove_reagent(type, adjusted_metabolization_rate) //By default it slowly disappears.
		if(M.client)
			if(!istype(src, /datum/reagent/drug) && reagent_state == LIQUID)
				record_featured_object_stat(FEATURED_STATS_DRINKS, name, adjusted_metabolization_rate)
			if(istype(src, /datum/reagent/consumable/ethanol))
				record_featured_stat(FEATURED_STATS_ALCOHOLICS, M, adjusted_metabolization_rate)
				record_round_statistic(STATS_ALCOHOL_CONSUMED, adjusted_metabolization_rate)
			if(istype(src, /datum/reagent/water))
				record_round_statistic(STATS_WATER_CONSUMED, adjusted_metabolization_rate)

/datum/reagent/proc/on_transfer(atom/A, method=TOUCH, trans_volume) //Called after a reagent is transfered
	if(iscarbon(A))
		SEND_SIGNAL(A, COMSIG_CARBON_REAGENT_ADD, src, trans_volume, method)
	return

/datum/reagent/proc/set_quality(new_quality)
	LAZYSET(data, "quality", new_quality)

/// Use this proc to lazyaccess quality data and keep data uninitialized.
/datum/reagent/proc/get_recipe_quality()
	var/recipe_quality = LAZYACCESS(data, "quality")
	return recipe_quality ? recipe_quality : base_recipe_quality

// Called when this reagent is first added to a mob
/datum/reagent/proc/on_mob_add(mob/living/affected_mob)
	if(added_traits)
		affected_mob.add_traits(added_traits, "base:[type]")

// Called when this reagent is removed while inside a mob
/datum/reagent/proc/on_mob_delete(mob/living/affected_mob)
	REMOVE_TRAITS_IN(affected_mob, "base:[type]")

// Called when this reagent first starts being metabolized by a liver
/datum/reagent/proc/on_mob_metabolize(mob/living/affected_mob)
	SHOULD_CALL_PARENT(TRUE)
	if(metabolized_traits)
		affected_mob.add_traits(metabolized_traits, "metabolize:[type]")

// Called when this reagent stops being metabolized by a liver
/datum/reagent/proc/on_mob_end_metabolize(mob/living/affected_mob)
	SHOULD_CALL_PARENT(TRUE)
	REMOVE_TRAITS_IN(affected_mob, "metabolize:[type]")

/// Called when this liquid is aerated (sprinklers vents and pumps for now)
/datum/reagent/proc/on_aeration(volume, turf/turf)
	return

/// Called when a reagent is inside of a mob when they are dead
/datum/reagent/proc/on_mob_dead(mob/living/carbon/C, delta_time)
	if(!dead_head)
		return
	current_cycle++
	if(length(reagent_removal_skip_list))
		return
	holder.remove_reagent(type, metabolization_rate * C.metabolism_efficiency * delta_time)

/datum/reagent/proc/on_move(mob/M)
	return

// Called after add_reagents creates a new reagent.
/datum/reagent/proc/on_new(list/incoming_data)
	if(incoming_data)
		data = incoming_data

		if(!("quality" in data))
			data["quality"] = base_recipe_quality
		if("custom_name" in data)
			name = data["custom_name"]
		if("custom_scent" in data)
			scent_description = data["custom_scent"]
		if("custom_tastes" in data)
			taste_description = data["custom_tastes"]

/// Called when two reagents of the same are mixing. Reminder that incoming_data is not automatically assigned to data here.
/datum/reagent/proc/on_merge(list/incoming_data, other_volume)
	SHOULD_CALL_PARENT(TRUE)
	if(incoming_data && incoming_data["quality"]) //special handling for this
		LAZYINITLIST(data)
		var/current_quality = get_recipe_quality()
		var/other_quality = incoming_data["quality"]

		var/weighted_average = LERP(current_quality, other_quality, other_volume / volume)
		data["quality"] = weighted_average

/datum/reagent/proc/on_update(atom/A)
	return

// Called when the reagent container is hit by an explosion
/datum/reagent/proc/on_ex_act(severity)
	return

// Called if the reagent has passed the overdose threshold and is set to be triggering overdose effects
/datum/reagent/proc/overdose_process(mob/living/M)
	return

/datum/reagent/proc/overdose_start(mob/living/M)
	to_chat(M, "<span class='danger'>I feel like I took too much of [name]!</span>")
	M.add_stress(/datum/stress_event/overdose)

/datum/reagent/proc/addiction_act_stage1(mob/living/M)
	M.add_stress(/datum/stress_event/withdrawal_light)
	if(prob(30))
		to_chat(M, "<span class='notice'>I feel like having some [name] right about now.</span>")

/datum/reagent/proc/addiction_act_stage2(mob/living/M)
	M.add_stress(/datum/stress_event/withdrawal_medium)
	if(prob(30))
		to_chat(M, "<span class='notice'>I feel like you need [name]. You just can't get enough.</span>")

/datum/reagent/proc/addiction_act_stage3(mob/living/M)
	M.add_stress(/datum/stress_event/withdrawal_severe)
	if(prob(30))
		to_chat(M, "<span class='danger'>I have an intense craving for [name].</span>")

/datum/reagent/proc/addiction_act_stage4(mob/living/M)
	M.add_stress(/datum/stress_event/withdrawal_critical)
	if(prob(30))
		to_chat(M, "<span class='boldannounce'>You're not feeling good at all! You really need some [name].</span>")

/// Should return a associative list where keys are taste descriptions and values are strength ratios
/datum/reagent/proc/get_taste_description(mob/living/taster)
	return list("[taste_description]" = 1)

/datum/reagent/proc/add_to_member(obj/effect/abstract/liquid_turf/adder)
	return

/datum/reagent/proc/remove_from_member(obj/effect/abstract/liquid_turf/remover)
	return

/proc/pretty_string_from_reagent_list(list/reagent_list)
	//Convert reagent list to a printable string for logging etc
	var/list/rs = list()
	for (var/datum/reagent/R in reagent_list)
		rs += "[R.name], [R.volume]"

	return rs.Join(" | ")
